      SUBROUTINE CIRCUM2(IERROR,ntri,XYZ_CENTRE1,XYZ_TRI,
     '  ONBOUND,ERROR,*)

C#### Subroutine: CIRCUM2
C###  Description:
C###    CIRCUM2 calculates the circumcentre or centre of a circle
C###    generated by a triangle. Used when created the pulmonary
C###    capillary mesh. Boundary triangles and triangles on far
C###    side of sphere can cause problems in these cases the centre
C###    is used in place of the circumcentre. XYZ_CENTRE1 contains
C###    co-ordinates for the centre which will be used.

C*** KSB June, 2002.

      IMPLICIT NONE
      INCLUDE 'geom00.cmn'
      INCLUDE 'genmesh.cmn'
!     Parameter List
      INTEGER IERROR,ntri
      REAL*8 XYZ_CENTRE1(3),XYZ_TRI(3,3)
      LOGICAL ONBOUND(MAX_TRIANGLES)
      CHARACTER ERROR*(*)
!     Local variables
      INTEGER j,ntri2
      REAL*8 LENGTH,XYZ_CENTRE2(3)

      CALL ENTERS('CIRCUM2',*9999)

C       Calculate circumcentre
      CALL SPHERE_CALC_CIRCUM(XYZ_TRI(1,1),XYZ_TRI(1,2),
     '  XYZ_TRI(1,3),XYZ_CENTRE1,IERROR,ERROR,*9999) !calc ccentre
C       Boundary triangles cause problems:use averaged nodal coordinates
      DO j=1,3
        XYZ_CENTRE2(j)=0.d0
      ENDDO !j
      DO ntri2=1,3 !for each vertex
        DO j=1,3
          XYZ_CENTRE2(j)=XYZ_CENTRE2(j)+XYZ_TRI(j,ntri2)/3.d0
        ENDDO !j
      ENDDO !ntri2
      LENGTH=0.d0
      DO j=1,3
        LENGTH=LENGTH+XYZ_CENTRE2(j)**2.d0
      ENDDO
      LENGTH=DSQRT(LENGTH)
      DO j=1,3
        XYZ_CENTRE2(j)=XYZ_CENTRE2(j)/LENGTH !to surface of unit sfr
      ENDDO
      LENGTH=0.d0
      DO j=1,3
        LENGTH=LENGTH+(XYZ_CENTRE1(j)-XYZ_CENTRE2(j))**2.d0
      ENDDO !j
      LENGTH=DSQRT(LENGTH)
      IF(LENGTH.GT.0.5d0.OR.ONBOUND(ntri))THEN
C... on other side of circle, use xyz2, or boundary triangle
C... Boundary triangles cause problems:use averaged nodal coordinates
        DO j=1,3
          XYZ_CENTRE1(j)=XYZ_CENTRE2(j)
        ENDDO
      ENDIF

      CALL EXITS('CIRCUM2')
      RETURN
 9999 CALL ERRORS('CIRCUM2',ERROR)
      CALL EXITS('CIRCUM2')
      RETURN 1
      END

