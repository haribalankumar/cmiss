      SUBROUTINE WRITE_BIN_TAG_HEADER(FILEID,ERROR,*)

C#### Subroutine: WRITE_BIN_TAG_HEADER
C###  Description:
C###    WRITE_BIN_TAG_HEADER writes a tag header to the binary file
C###    given by FILEID.

      IMPLICIT NONE
      INCLUDE 'b01.cmn'
      INCLUDE 'cbdi02.cmn'
      INCLUDE 'binf00.cmn'
      INCLUDE 'mach00.inc'
!     Parameter List
      INTEGER FILEID
      CHARACTER ERROR*(*)
!     Local Variables
      INTEGER IBEG,IEND

      CALL ENTERS('WRITE_BIN_TAG_HEADER',*9999)

      CALL STRING_TRIM(TAGHEADER,IBEG,IEND)
      IF(IBEG.GT.IEND) THEN
        TAGHEADERBYTES=0
      ELSE IF(IBEG.EQ.IEND.AND.TAGHEADER(IBEG:IEND).EQ.' ') THEN
        TAGHEADERBYTES=0
      ELSE
        TAGHEADERBYTES=IEND-IBEG+1
      ENDIF

      INTDATA(1)=TAGINDEX
      INTDATA(2)=TAGHEADERBYTES
      CALL BINWRITEFILE(FILEID,INTTYPE,2,INTDATA,REAL4DATA,
     '  REAL8DATA,CHARDATA,LOGDATA,SINTDATA,ERROR,*9999)
      CALL BINWRITEFILE(FILEID,CHARTYPE,TAGHEADERBYTES,INTDATA,
     '  REAL4DATA,REAL8DATA,TAGHEADER,LOGDATA,SINTDATA,ERROR,*9999)

      INTDATA(1)=NUMBERSUBTAGS
      CALL BINWRITEFILE(FILEID,INTTYPE,1,INTDATA,REAL4DATA,
     '  REAL8DATA,TAGHEADER,LOGDATA,SINTDATA,ERROR,*9999)

      IF(NUMBERSUBTAGS.EQ.0) THEN
        INTDATA(1)=NUMTAGBYTES
        CALL BINWRITEFILE(FILEID,INTTYPE,1,INTDATA,REAL4DATA,
     '    REAL8DATA,TAGHEADER,LOGDATA,SINTDATA,ERROR,*9999)
      ENDIF

      IF(DOP) THEN
C KAT 14May01: Can't branch out of critical section.
C              Critical section is not essential.
CC$      call mp_setlock()
        WRITE(OP_STRING,'('' Tag index : '',I5,'
     '    //'/'' Tag header bytes : '',I5)') TAGINDEX,TAGHEADERBYTES
        CALL WRITES(IODI,OP_STRING,ERROR,*9999)
        IF(TAGHEADERBYTES.GT.0) THEN
          WRITE(OP_STRING,'('' Tag Header: '',A)')
     '      TAGHEADER(1:TAGHEADERBYTES)
          CALL WRITES(IODI,OP_STRING,ERROR,*9999)
        ENDIF
        WRITE(OP_STRING,'('' Number of sub tags : '',I5,'
     '    //'/'' Number of tag bytes : '',I10)') NUMBERSUBTAGS,
     '    NUMTAGBYTES
        CALL WRITES(IODI,OP_STRING,ERROR,*9999)
CC$      call mp_unsetlock()
      ENDIF

      CALL EXITS('WRITE_BIN_TAG_HEADER')
      RETURN
 9999 CALL ERRORS('WRITE_BIN_TAG_HEADER',ERROR)
      CALL EXITS('WRITE_BIN_TAG_HEADER')
      RETURN 1
      END


