      SUBROUTINE BINWRITEFILE(FILEID,DATA_TYPE,NUM_DATA,IDATA,
     '  SRDATA,DRDATA,CDATA,LDATA,SIDATA,ERROR,*)

C#### Subroutine: BINWRITEFILE
C###  Description:
C###    BINWRITEFILE writes num_data lots of data (of type
C###    given by data_type) from the data array to a binary file
C###    specified by FILEID.

      IMPLICIT NONE
      INCLUDE 'mach00.cmn'
      INCLUDE 'mach00.inc'
!     Parameter List
      INTEGER FILEID,DATA_TYPE,NUM_DATA,IDATA(*)
      REAL SRDATA(*)
      REAL*8 DRDATA(*)
      CHARACTER CDATA*(*),ERROR*(*)
      LOGICAL LDATA(*)
      INTEGER*2 SIDATA(*)
!     Local Variables
      INTEGER CERROR(50),CERRLEN,CSTRING(250),ENDIAN,ERR

      CALL ENTERS('BINWRITEFILE',*9999)

      IF(DATA_TYPE.NE.CHARTYPE) THEN
        IF(ENDIANTYPE.EQ.CHAR(MACH_BIGENDIAN)) THEN
          ENDIAN=0
        ELSE
          ENDIAN=1
        ENDIF
      ELSE
        ENDIAN=0
      ENDIF

      IF(DATA_TYPE.EQ.INTTYPE) THEN !Integer data
        CALL BINARYWRITEFILE(FILEID,ENDIAN,NUM_DATA,DATA_TYPE,IDATA,
     '    ERR,CERROR)
      ELSE IF(DATA_TYPE.EQ.SPTYPE) THEN !Real*4 data
        CALL BINARYWRITEFILE(FILEID,ENDIAN,NUM_DATA,DATA_TYPE,SRDATA,
     '    ERR,CERROR)
      ELSE IF(DATA_TYPE.EQ.DPTYPE) THEN !Real*8 data
        CALL BINARYWRITEFILE(FILEID,ENDIAN,NUM_DATA,DATA_TYPE,DRDATA,
     '    ERR,CERROR)
      ELSE IF(DATA_TYPE.EQ.CHARTYPE) THEN !character data
        CALL F2CSTRING(CSTRING,CDATA)
        CALL BINARYWRITEFILE(FILEID,ENDIAN,NUM_DATA,DATA_TYPE,CSTRING,
     '    ERR,CERROR)
      ELSE IF(DATA_TYPE.EQ.LOGTYPE) THEN !logical data
        CALL BINARYWRITEFILE(FILEID,ENDIAN,NUM_DATA,DATA_TYPE,LDATA,
     '    ERR,CERROR)
      ELSE IF(DATA_TYPE.EQ.SINTTYPE) THEN !short integer data
        CALL BINARYWRITEFILE(FILEID,ENDIAN,NUM_DATA,DATA_TYPE,SIDATA,
     '    ERR,CERROR)
      ELSE
        ERROR='>>Invalid data type'
        GOTO 9999
      ENDIF
      IF(ERR.NE.0) THEN
        CALL CSTRINGLEN(CERRLEN,CERROR)
        CALL C2FSTRING(CERROR,CERRLEN,ERROR)
        GOTO 9999
      ENDIF

      CALL EXITS('BINWRITEFILE')
      RETURN
 9999 CALL ERRORS('BINWRITEFILE',ERROR)
      CALL EXITS('BINWRITEFILE')
      CALL BINARYCLOSEFILE(FILEID,ERR,CERROR)
      RETURN 1
      END


