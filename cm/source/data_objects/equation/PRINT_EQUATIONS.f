      SUBROUTINE PRINT_EQUATIONS(ERROR,*)

C#### Subroutine: PRINT_EQUATION
C###  Description:
C###    Prints equation details.

      IMPLICIT NONE
      INCLUDE 'cbdi02.cmn'
      INCLUDE 'colours00.cmn'
      INCLUDE 'equation00.cmn'

!     Parameter List
      CHARACTER ERROR*(*)

!     Local Variables
      INTEGER CINDEX,CTOTAL,NINDEX,NTOTAL,PINDEX,PTOTAL
      INTEGER IB1,IB2,IB3,IE1,IE2,IE3
      REAL*8 VALUE
      CHARACTER ILABEL*(EQN_IN_LABELS_LEN),
     &  MAP_CHILD*(EQN_IN_MAP_CHILD_LEN),
     &  MAP_PARENT*(EQN_IN_MAP_PARENT_LEN),
     &  MAP_STRING*(255),
     &  MAP_TYPE*(EQN_IN_MAP_TYPE_LEN),
     &  NLABEL*(EQUATION_NOTE_LEN),
     &  OLABEL*(EQN_OUT_LABELS_LEN),
     &  PLABEL*(EQUATION_LABELS_LEN)

      CALL ENTERS('PRINT_EQUATIONS',*9999)      
          
      CALL GET_EQUATION_TOTAL(PTOTAL,ERROR,*9999)
      
      DO PINDEX=1,PTOTAL
        CALL GET_EQUATION_LABEL(PINDEX,PLABEL,ERROR,*9999)
        
        CALL STRING_TRIM(PLABEL,IB1,IE1)
        WRITE(OP_STRING,'(''EQUATION:'//COLOUR_PARENT//' '
     &    //PLABEL(IB1:IE1)//COLOUR_DEFAULT//''')')
        CALL WRITES(IOFI,OP_STRING,ERROR,*9999)

        CALL GET_EQUATION_NOTE_TOTAL(PLABEL,NTOTAL,ERROR,*9999)
        DO NINDEX=1,NTOTAL
          CALL GET_EQUATION_NOTE(PLABEL,
     &      NINDEX,NLABEL,ERROR,*9999)
          CALL STRING_TRIM(NLABEL,IB1,IE1)
          WRITE(OP_STRING,'(7X,'''//NLABEL(IB1:IE1)//''')')
          CALL WRITES(IOFI,OP_STRING,ERROR,*9999)
        ENDDO

        WRITE(OP_STRING,'(4X,''Inputs:'')')
        CALL WRITES(IOFI,OP_STRING,ERROR,*9999)

        CALL GET_EQUATION_INPUT_TOTAL(PLABEL,CTOTAL,ERROR,*9999)
        
        DO CINDEX=1,CTOTAL
          CALL GET_EQUATION_INPUT_LABEL(PLABEL,CINDEX,ILABEL,
     &      ERROR,*9999)
          
          CALL GET_EQUATION_INPUT_MAP(PLABEL,ILABEL,MAP_TYPE,
     &      MAP_PARENT,MAP_CHILD,ERROR,*9999)
          IF(MAP_TYPE.EQ.'INITIAL_VALUE')THEN
            CALL GET_EQUATION_INPUT_INIT_VALUE(PLABEL,ILABEL,VALUE,
     &        ERROR,*9999)
            WRITE(MAP_STRING,'('' = '',D12.5)') VALUE
          ELSE
            CALL STRING_TRIM(MAP_TYPE,IB1,IE1)
            CALL STRING_TRIM(MAP_PARENT,IB2,IE2)
            CALL STRING_TRIM(MAP_CHILD,IB3,IE3)
            WRITE(MAP_STRING,'('''//' ---map--> '//MAP_TYPE(IB1:IE1)
     &        //COLOUR_DEFAULT//' : '
     &        //COLOUR_PARENT//MAP_PARENT(IB2:IE2)
     &        //COLOUR_DEFAULT//' : '
     &        //COLOUR_CHILD//MAP_CHILD(IB3:IE3)
     &        //COLOUR_DEFAULT//''')')
          ENDIF
              
          CALL STRING_TRIM(ILABEL,IB1,IE1)
          CALL STRING_TRIM(MAP_STRING,IB2,IE2)
          WRITE(OP_STRING,'(7X,'''//COLOUR_CHILD//ILABEL(IB1:IE1)
     &      //COLOUR_DEFAULT//' '//MAP_STRING(IB2:IE2)//''')')
          CALL WRITES(IOFI,OP_STRING,ERROR,*9999)
        ENDDO
        
        WRITE(OP_STRING,'(4X,''Outputs:'')')
        CALL WRITES(IOFI,OP_STRING,ERROR,*9999)

        CALL GET_EQUATION_OUTPUT_TOTAL(PLABEL,CTOTAL,ERROR,*9999)
        
        DO CINDEX=1,CTOTAL
          CALL GET_EQUATION_OUTPUT_LABEL(PLABEL,CINDEX,OLABEL,
     &      ERROR,*9999)
          CALL STRING_TRIM(OLABEL,IB1,IE1)
          WRITE(OP_STRING,'(7X,'''//COLOUR_CHILD//OLABEL(IB1:IE1)
     &      //COLOUR_DEFAULT//''')')
          CALL WRITES(IOFI,OP_STRING,ERROR,*9999)
        ENDDO
        WRITE(OP_STRING,'()')
        CALL WRITES(IOFI,OP_STRING,ERROR,*9999)
      ENDDO 

      CALL EXITS('PRINT_EQUATIONS')
      RETURN
 9999 CALL ERRORS('PRINT_EQUATIONS',ERROR)
      CALL EXITS('PRINT_EQUATIONS')
      RETURN 1
      END


